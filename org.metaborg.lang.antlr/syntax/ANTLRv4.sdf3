module ANTLRv4

imports

  Common
  Comment
  Literal

context-free start-symbols

  Start Rule EmptyElement Element

context-free syntax

  Start.Grammar = <grammar <ID>;

<{Rule "\n\n"}+>>

  Rule.Rule = <<Fragment?><ID>:
  <EmptyElement>
;>

  Fragment.Fragment = <fragment >

  EmptyElement.Empty = <>
  EmptyElement = Element

  Element = <(<Element>)> {bracket}
  Element.Alt = <<EmptyElement> | <EmptyElement>> {left}
  Element.Conc = <<Element> <Element>> {left}
  Element.Star = <<Element>*>
  Element.Opt = <<Element>?>
  Element.Plus = <<Element>+>
  Element.Literal = Literal
  Element.Nonterminal = ID
  Element.EOF = <EOF>
  Element.Wildcard = Wildcard
  Element.DottedRange = <<DottedRangeChar>..<DottedRangeChar>>
  Element.Command = <<Element> -\> <ID>>
  Element = CharacterClass

  CharacterClass.CharClass = <[<Ranges>]>
  CharacterClass.NegClass = <~<CharacterClass>>

  Ranges.RangeConc = <<Ranges><Ranges>> {left}
  Ranges = Range

  Range.CharRange = <<RangeChar>-<RangeChar>>
  Range = Character

  Character.Single = Char

lexical syntax

  Wildcard = "."

  DottedRangeChar = "'" RangeChar "'"

  RangeChar = [a-zA-Z0-9]

  Char = [a-zA-Z0-9]
  Char = [\$\_\'\"]
  Char = "\\" [ntr]
  Char = "\\\\"

lexical restrictions

  Wildcard -/- [\.]

context-free priorities

  {left: Element.Star Element.Opt Element.Plus} >
  Element.Conc >
  Element.Alt >
  Element.Command
