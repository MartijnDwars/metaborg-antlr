module ANTLRv4

imports
  
  Common
  Comment
  Literal

context-free start-symbols
  
  Start

context-free syntax
  
  Start.Grammar = <grammar <ID>;

<{Rule "\n\n"}+>>

  Rule.Rule = <<Fragment?><ID>:
  <ElementOpt>
;>

  Fragment.Fragment = <fragment >
  
  ElementOpt.Empty = 
  ElementOpt = Element

  Element = <(<Element>)> {bracket}
  Element.Conc = <<Element> <Element>> {left}
  Element.Alt = <<Element> | <Element>> {left}
  Element.Star = <<Element>*>
  Element.Opt = <<Element>?>
  Element.Plus = <<Element>+>
  Element.Literal = <'<Literal>'>
  Element.Nonterminal = ID
  Element.EOF = <EOF>
  Element = CharacterClass
  
  CharacterClass.CharClass = <[<Ranges>]>
  CharacterClass.NegClass = <~<CharacterClass>>

  Ranges.RangeConc = <<Ranges><Ranges>> {left}
  Ranges = Range

  Range.CharRange = <<RangeChar>-<RangeChar>>
  Range = Character
  
  Character.Single = Char

lexical syntax

  RangeChar = [a-zA-Z0-9]
  Char = RangeChar | [\$\_]

context-free priorities
  
  {left: Element.Star Element.Opt Element.Plus} >
  Element.Conc >
  Element.Alt
